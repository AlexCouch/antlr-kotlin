buildscript {
    ext.kotlin_version = '1.3.31'
    ext.projectVersion = '0.0.3'
    ext.antlrKotlinVersion = projectVersion

    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.strumenta.antlr-kotlin:antlr-kotlin-gradle-plugin:$projectVersion"
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'com.strumenta.antlrkotlin'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile "com.strumenta.antlr-kotlin:antlr-kotlin-runtime-common:$projectVersion"
    compile "com.strumenta.antlr-kotlin:antlr-kotlin-runtime-jvm:$projectVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    //testCompile 'junit:junit:4.12'
}

generateKotlinGrammarSource {
    version = projectVersion
    maxHeapSize = "1024m"
    arguments += ['-package', 'me.tomassetti.minicalc', '-visitor']
    outputDirectory = new File("generated-src/antlr/".toString())
}
compileKotlin.dependsOn generateKotlinGrammarSource
sourceSets {
    generated {
        kotlin.srcDir 'generated-src/antlr/'
    }
}
compileKotlin.source sourceSets.generated.kotlin, sourceSets.main.kotlin

clean{
    delete "generated-src"
}

idea {
    module {
        sourceDirs += file("generated-src/antlr/")
    }
}